import com.android.build.gradle.internal.api.ApkVariantOutputImpl
import java.util.Properties
import java.io.FileInputStream

plugins {
    id("com.android.application")
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id("dev.flutter.flutter-gradle-plugin")
}

// Load signing credentials from key.properties (not committed to git).
// Falls back to debug signing if the file doesn't exist (CI / first checkout).
val keyPropertiesFile = rootProject.file("key.properties")
val keyProperties = Properties()
if (keyPropertiesFile.exists()) {
    keyProperties.load(FileInputStream(keyPropertiesFile))
}

android {
    namespace = "com.schierer.arcade.graphical_bsdgames"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    // Don't embed the encrypted Google Play dependency-metadata signing
    // block — F-Droid rejects APKs containing opaque binary blobs.
    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    signingConfigs {
        if (keyPropertiesFile.exists()) {
            create("release") {
                keyAlias     = keyProperties["keyAlias"]    as String
                keyPassword  = keyProperties["keyPassword"] as String
                storeFile    = file(keyProperties["storeFile"] as String)
                storePassword= keyProperties["storePassword"] as String
            }
        }
    }

    defaultConfig {
        applicationId = "com.schierer.arcade.graphical_bsdgames"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    buildTypes {
        release {
            signingConfig = if (keyPropertiesFile.exists())
                signingConfigs.getByName("release")
            else
                signingConfigs.getByName("debug")
        }
    }
}

// ABI-split version-code scheme for F-Droid. When building with
// `flutter build apk --split-per-abi`, each per-ABI APK needs a distinct
// version code. F-Droid's metadata mirrors this with
// `VercodeOperation: %c * 10 + N`, so the codes must match: baseCode * 10 + abi.
val abiCodes = mapOf("armeabi-v7a" to 1, "arm64-v8a" to 2, "x86_64" to 3)
android.applicationVariants.configureEach {
    val variant = this
    variant.outputs.forEach { output ->
        val abiVersionCode = abiCodes[output.filters.find { it.filterType == "ABI" }?.identifier]
        if (abiVersionCode != null) {
            (output as ApkVariantOutputImpl).versionCodeOverride =
                variant.versionCode * 10 + abiVersionCode
        }
    }
}

kotlin {
    compilerOptions {
        jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
    }
}

flutter {
    source = "../.."
}
